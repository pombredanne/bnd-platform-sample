
apply plugin: org.standardout.gradle.plugin.platform.PlatformPlugin
apply plugin: org.standardout.gradle.plugin.include.IncludePlugin

defaultTasks 'clean', 'updateSite'

repositories {
	maven {
		// must be first (before maven central) so javax.media:jai-core can be resolved
		url 'http://download.osgeo.org/webdav/geotools/'
	}
	mavenLocal()
	mavenCentral()
	maven {
		url "http://repo.aduna-software.org/maven2/releases"
	}
	maven {
		url "http://fortytwo.net/maven2"
	}
	// ivy {   url "http://repo.mycompany.com/repo"}
}

// project version is also platform feature version default
version = '1.2.0'

project.ext {
	// project specific variables
	groovyVersion = '2.1.5'
	schemacrawlerVersion = '8.16'
}

configurations {
	platform {
		// groovy
		resolutionStrategy.eachDependency { DependencyResolveDetails details ->
			if (details.requested.group == 'org.codehaus.groovy') {
				// always use groovy-all instead of (groovy-*)
				details.useTarget "org.codehaus.groovy:groovy-all:${groovyVersion}"
			}
		}
	}
}

include {
	// include modules with platform configuration
	location('modules/logging.groovy') {
		slf4jAndLogback '1.7.2', '1.0.10' // slf4j and logback with customized versions
	}
	location('modules/geotools.groovy') {
		geotools()
	}
	from 'modules/orientdb.groovy', {
		orientGraphDB()
	}
	from 'modules/common.groovy'
	from 'modules/schemacrawler.groovy'
}

platform {
	// platform extension - plugin specific configuration
	
	// fetch sources for dependencies if possible (result in source bundles)
	fetchSources = true
	
	// extend/override the default bnd configuration
	// the default bnd configuration does not apply to Jars that are already bundles
	// (to protect the existing imports and exports)
	bnd {
		// sun.* and com.sun.* packages are optional by default
		optionalImport 'sun.*', 'com.sun.*',
			// unit tests only dependencies should be optional as well 
			'junit.framework', 'org.junit'
	}
	
	// dependencies can be declared in the extension
	// (in fact, they need to be, if a custom bnd configuration is needed)
	
	// groovy
	bundle "org.codehaus.groovy:groovy-all:${groovyVersion}"
	
	// security
	bundle 'org.jasig.cas:cas-client:3.1.10'
	
	// utilities
	bundle 'com.google.guava:guava:16.0.1'
	bundle 'net.sf.trove4j:trove4j:3.0.3'
	bundle 'commons-io:commons-io:2.4'
	bundle 'net.sf.ehcache:ehcache-core:2.6.6', {
		bnd {
			// make hibernate packages optional
			optionalImport 'org.hibernate', 'org.hibernate.*'
		}
	}
	bundle group: 'org.pegdown', name: 'pegdown', version: '1.4.2'
	
	// local dependencies
	
	// single file
	bundle file('libs/groovy-sandbox-1.6-SNAPSHOT.jar'), {
		bnd {
			version = '1.6.0.hale'
			symbolicName = 'org.kohsuke.groovy.sandbox'
			bundleName = 'Groovy Sandbox (adapted)'
			instruction 'Export-Package', "*;version=$version"
		}
	}
	
	// bnd configurations (just configuration, not adding a dependency)
	bnd group: 'xml-apis', name: 'xml-apis', {
		instruction 'Private-Package', 'license' // we don't want to export 'license' as package
	}
	
	// later bnd configurations augment and possibly override previously defined configuration
	// they do not override, if they are more general (e.g. only name defined instead of group and name)
	bnd name: 'xml-apis', {
		bundleName = 'XML APIs' // set's bundle name
		instruction 'Private-Package', '' // does not override instruction in previous bnd 
	}
}

dependencies {
	// platform dependencies can also be defined here (w/o bnd configuration)
	
	platform 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
	
	// XML
	platform 'org.apache.ws.xmlschema:xmlschema-core:2.0.2'
//	platform 'org.apache.ws.commons.schema:XmlSchema:1.4.7'
	
	// jackson 1
	platform 'org.codehaus.jackson:jackson-core-asl:1.9.13'
	// jackson 2
	platform 'com.fasterxml.jackson.core:jackson-core:2.3.0'
	platform 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}